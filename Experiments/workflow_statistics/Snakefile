import numpy as np
import pandas as pd

nseeds = 100
seeds = list(range(nseeds))
n_cells_COMPASS=2000
n_cells_BITSC2=100
n_nodes = [8] 
n_SNVs= [7] 
n_CNVs= [3]
dropout_sigma= 0.0
nodeprob_concentration= 10
regionprobs_sigma = [0.5]
theta = 1000
depth=20

outdir = "out/"
tmpdir = "tmp/"

rule all:
    params:
        threads="1",
        runtime="10",
        memory="1000"
    input:
        "error_statistics.csv"


rule combine_results:
    params:
        threads="1",
        runtime="10",
        memory="1000"
    input:
        expand(outdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{rhosig}rhosigma_error_statistics.txt", nnodes=n_nodes,nSNVs=n_SNVs,nCNVs=n_CNVs,rhosig=regionprobs_sigma)
    output:
        "error_statistics.csv"
    shell:
        "cp {input} {output}"


rule compute_error_rates:
    params:
        threads="1",
        runtime="10",
        memory="1000"
    input:
        expand(outdir+"{{sample}}_{seed}_treeTRUE.gv",seed=seeds),
        expand(outdir+"{{sample}}_{seed}_treeCOMPASS.gv",seed=seeds),
        expand(outdir+"{{sample}}_{seed}_treeBITSC2.csv",seed=seeds),
        expand(outdir+"{{sample}}_{seed}_originsBITSC2.csv",seed=seeds)
    output:
        outdir+"{sample}_error_statistics.txt"
    shell:
        "python compute_error_stats.py -i {outdir}{wildcards.sample} --nseeds {nseeds} -o {output}"


rule generate_synthetic_data:
    params:
        threads="1",
        runtime="10",
        memory="1000"
    output:
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{ncells}cells_{rhosig}rhosigma_{seed}_variants.csv",
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{ncells}cells_{rhosig}rhosigma_{seed}_regions.csv",
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{ncells}cells_{rhosig}rhosigma_{seed}_AD.csv",
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{ncells}cells_{rhosig}rhosigma_{seed}_DP.csv",
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{ncells}cells_{rhosig}rhosigma_{seed}_tree.gv"
    shell:
        "python generate_synthetic_data_stats.py -o {tmpdir}syn_{wildcards.nnodes}nodes_{wildcards.nSNVs}SNVs_{wildcards.nCNVs}CNVs_{wildcards.ncells}cells_{wildcards.rhosig}rhosigma_{wildcards.seed} "\
        "--ncells {wildcards.ncells} --nnodes {wildcards.nnodes} --nSNVs {wildcards.nSNVs} --nCNVs {wildcards.nCNVs} " \
        "--dropoutsigma {dropout_sigma} --nodeprobconcentration {nodeprob_concentration} --depth {depth} --theta {theta} --regionprobsigma {wildcards.rhosig} --seed {wildcards.seed}"

rule cp_true_tree:
    params:
        threads="1",
        runtime="10",
        memory="1000"
    input:
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_"+str(n_cells_COMPASS)+"cells_{rhosig}rhosigma_{seed}_tree.gv"
    output:
        outdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{rhosig}rhosigma_{seed}_treeTRUE.gv"
    shell: 
        "cp {input} {output}"

rule infer_tree_COMPASS:
    params:
        threads="4",
        runtime="50",
        memory="6000"
    input:
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_"+str(n_cells_COMPASS)+"cells_{rhosig}rhosigma_{seed}_variants.csv",
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_"+str(n_cells_COMPASS)+"cells_{rhosig}rhosigma_{seed}_regions.csv"
    output:
        outdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{rhosig}rhosigma_{seed}_treeCOMPASS.gv"
    shell:
        "./COMPASS -o {output} -i {tmpdir}syn_{wildcards.nnodes}nodes_{wildcards.nSNVs}SNVs_{wildcards.nCNVs}CNVs_{n_cells_COMPASS}cells_{wildcards.rhosig}rhosigma_{wildcards.seed} --nchains 4 --chainlength 5000 --burnin 2000 --temperature 10 -d 0 --filterregions 0"

rule infer_tree_BITSC2:
    params:
        runtime="4:00",
        threads="1",
        memory="1000"
    input:
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_"+str(n_cells_BITSC2)+"cells_{rhosig}rhosigma_{seed}_DP.csv",
        tmpdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_"+str(n_cells_BITSC2)+"cells_{rhosig}rhosigma_{seed}_AD.csv"
    output:
        outdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{rhosig}rhosigma_{seed}_originsBITSC2.csv",
        outdir+"syn_{nnodes}nodes_{nSNVs}SNVs_{nCNVs}CNVs_{rhosig}rhosigma_{seed}_treeBITSC2.csv"
    shell:
        "Rscript --vanilla BITSC2/code/BiTSC2_run_script.R {tmpdir}syn_{wildcards.nnodes}nodes_{wildcards.nSNVs}SNVs_{wildcards.nCNVs}CNVs_{n_cells_BITSC2}cells_{wildcards.rhosig}rhosigma_{wildcards.seed} "\
        "{outdir}syn_{wildcards.nnodes}nodes_{wildcards.nSNVs}SNVs_{wildcards.nCNVs}CNVs_{wildcards.rhosig}rhosigma_{wildcards.seed} {depth} 4000 {wildcards.nnodes}"
